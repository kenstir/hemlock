plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

// load signing keys from a secret file
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('secret/keystore.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    keystoreProperties.setProperty('storeFile', '/dev/null')
}

android {
    compileSdk 35
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        multiDexEnabled = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        versionName rootProject.ext.globalVersionName
        versionCode rootProject.ext.globalVersionCode
    }
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['originalUploadKeyAlias']
            keyPassword keystoreProperties['originalUploadKeyPassword']
        }
    }
    buildTypes {
        release {
            /// https://developer.android.com/studio/build/shrink-code

            // enable code shrinking, obfuscating, and optimization
            // see also {dontshrink|dontobfuscate|dontoptimize} in proguard-rules.pro
            minifyEnabled true

            // enable resource shrinking
            shrinkResources = true

            // include ProGuard rules files
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // use signingConfig defined above
            signingConfig = signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace = 'org.cwmars'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':core')
    implementation 'androidx.multidex:multidex:2.0.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    testImplementation 'junit:junit:4.13.2'

    // include pro assets if directory exists
    def dir = new File('assets/pro')
    if (dir.exists()) {
        implementation project(path: ':assets:pro')
    }
}
